#version 450
#extension GL_ARB_separate_shader_objects                : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : enable

#include "camera.h"
#include "unpack.h"
#include "../common.h"

layout(local_size_x = SURFEL_UPDATE_GROUP_SIZE,
       local_size_y = 1,
       local_size_z = 1) in;

// camera information
layout(set = 0, binding = SCENE_CAMERA_BINDING) uniform
Camera { CameraInfo data; } camera;

// surfel information
layout(set = 1, binding = SURFEL_BINDING) buffer
SurfelBuffer { Surfel data[]; } surfels;

// surfel alive
layout(set = 1, binding = SURFEL_LIVE_BINDING) buffer
SurfelLiveBuffer { uint data[]; } surfelLive;

// surfel grids
layout(set = 1, binding = SURFEL_GRID_BINDING) buffer
SurfelGridBuffer { SurfelGridCell data[]; } surfelGrids;

// surfel cells
layout(set = 1, binding = SURFEL_CELL_BINDING) buffer
SurfelCellBuffer { uint data[]; } surfelCells;

// surfel stat
layout(set = 1, binding = SURFEL_STAT_BINDING) buffer
SurfelStatBuffer { SurfelStat data; } surfelStat;

/**
 * This compute shader performs the following:
 * 1. add surfel to surfel grid cell acceleration structure
 */
void main() {
    // check surfel availability
    uint currentSurfelID = gl_GlobalInvocationID.x;
    uint currentSurfelCount = surfelStat.data.count;
    if (currentSurfelID >= currentSurfelCount) {
        return;
    }

    Surfel surfel = surfels.data[currentSurfelID];
    vec3 surfelN = unpack_snorm3(surfel.normal);

    // fill grid cell content
    vec3 minBound = surfel.position - surfel.radius;
    vec3 maxBound = surfel.position + surfel.radius;
    int3 cell0 = compute_surfel_grid(camera.data, minBound);
    int3 cell1 = compute_surfel_grid(camera.data, maxBound);
    for (int i = cell0.x; i <= cell1.x; i++) {
        for (int j = cell0.y; j <= cell1.y; j++) {
            for (int k = cell0.z; k <= cell1.z; k++) {
                int3 gridIndex = int3(i, j, k);
                uint cellIndex = compute_surfel_cell(gridIndex);
                if (surfel_intersects_with_grid(surfel.position, surfelN, gridIndex)) {
                    SurfelGridCell cell = surfelGrids.data[cellIndex];
                    if (cell.count < SURFEL_CELL_CAPACITY) {
                        uint index = atomicAdd(surfelGrids.data[cellIndex].count, 1);
                        surfelCells.data[cell.offset + index] = currentSurfelID;
                    }
                }
            }
        }
    }
}
