#version 450
#extension GL_ARB_separate_shader_objects                : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : enable

#define ENABLE_SURFEL_DEBUG_POINT
// #define ENABLE_SURFEL_DEBUG_RANDOM
// #define ENABLE_SURFEL_DEBUG_DEPTH

#include "pack.h"
#include "unpack.h"
#include "camera.h"
#include "random.h"
#include "colors.h"
#include "../common.h"

layout(local_size_x = SURFEL_TILE_X,
       local_size_y = SURFEL_TILE_Y,
       local_size_z = 1) in;

// frame information
layout(set = 0, binding = SCENE_FRAME_BINDING) uniform
Frame { FrameInfo data; } frame;

// camera information
layout(set = 0, binding = SCENE_CAMERA_BINDING) uniform
Camera { CameraInfo data; } camera;

// surfel information
layout(set = 1, binding = SURFEL_BINDING) buffer
SurfelBuffer { Surfel data[]; } surfels;

// surfel alive
layout(set = 1, binding = SURFEL_LIVE_BINDING) buffer
SurfelLiveBuffer { uint data[]; } surfelLive;

// surfel alive
layout(set = 1, binding = SURFEL_DATA_BINDING) buffer
SurfelDataBuffer { SurfelData data[]; } surfelData;

// surfel grids
layout(set = 1, binding = SURFEL_GRID_BINDING) buffer
SurfelGridBuffer { SurfelGridCell data[]; } surfelGrids;

// surfel cells
layout(set = 1, binding = SURFEL_CELL_BINDING) buffer
SurfelCellBuffer { uint data[]; } surfelCells;

// surfel stat
layout(set = 1, binding = SURFEL_STAT_BINDING) buffer
SurfelStatBuffer { SurfelStat data; } surfelStat;

// surfel coverage
layout(set = 1, binding = SURFEL_COVERAGE_BINDING, r32f) uniform
image2D coverageImage;

// surfel depth atlas texture
layout(set = 1, binding = SURFEL_DEPTH_BINDING, rg32f) uniform
image2D surfelDepth;

// gbuffer diffuse
layout(set = 2, binding = GBUFFER_GLOBAL_DIFFUSE_BINDING, rgba32f) uniform
image2D diffuseImage;

// gbuffer depth
layout(set = 2, binding = GBUFFER_ALBEDO_BINDING) uniform
sampler2D albedoImage;

// gbuffer depth
layout(set = 2, binding = GBUFFER_DEPTH_BINDING) uniform
sampler2D depthImage;

// gbuffer object/instance
layout(set = 2, binding = GBUFFER_OBJECT_BINDING) uniform
usampler2D objectImage;

// gbuffer normal
layout(set = 2, binding = GBUFFER_NORMAL_BINDING) uniform
sampler2D normalImage;

#ifdef ENABLE_GBUFFER_WORLD_POSITION
// gbuffer position
layout(set = 2, binding = GBUFFER_POSITION_BINDING) uniform
sampler2D positionImage;
#endif

// debug surfel
layout(set = 3, binding = DEBUG_SURFEL_BINDING, rgba8) uniform
image2D debugImage;

// surfel variance
layout(set = 3, binding = DEBUG_SURFEL_VAR_BINDING, rgba8) uniform
image2D varianceImage;

shared uint groupMinSurfelID;
shared uint groupMinSurfelCoverage;

vec4 sample_radial_depth(vec2 pixel) {
    int2 base = int2(pixel / vec2(SURFEL_DEPTH_TEXELS)) * int2(SURFEL_DEPTH_TEXELS);
    vec2 tile = pixel - vec2(base) + vec2(0.5);
    int2 co = int2(tile);
    vec2 uv = fract(tile);

    int2 c0 = base + max(co - int2(1), int2(0));
    int2 c1 = base + min(co, int2(SURFEL_DEPTH_TEXELS - 1));
    vec4 p00 = imageLoad(surfelDepth, int2(c0.x, c0.y));
    vec4 p10 = imageLoad(surfelDepth, int2(c1.x, c0.y));
    vec4 p01 = imageLoad(surfelDepth, int2(c0.x, c1.y));
    vec4 p11 = imageLoad(surfelDepth, int2(c1.x, c1.y));
    return mix(mix(p00, p10, uv.x), mix(p01, p11, uv.x), uv.y);
}

/**
 * This compute shader performs the following:
 * 1. compute coverage for each pixel
 * 2. iteratively spawn new surfel based based on low coverage
 * 3. recycle surfels on high coverage
 */
void main() {
    groupMinSurfelID = 0xffffffff;
    groupMinSurfelCoverage = 0xffffffff;
    memoryBarrierShared();

    int2 pixel = int2(gl_GlobalInvocationID.xy);
    #ifdef ENABLE_HALFRES_LIGHT_APPLY
    pixel.x = pixel.x * 2;
    pixel.y = pixel.y * 2;
    #endif

    int2 resolution = int2(frame.data.resolution);

    // dimension check
    if (any(greaterThanEqual(pixel, resolution))) {
        return;
    }

    // calculate basic information
    float depth  = texelFetch(depthImage,  pixel, 0).x;
    uint  primID = texelFetch(objectImage, pixel, 0).x;
    vec3  N      = texelFetch(normalImage, pixel, 0).xyz;
    #ifdef ENABLE_GBUFFER_WORLD_POSITION
    vec3  P      = texelFetch(positionImage, pixel, 0).xyz;
    #else
    vec2  uv     = (vec2(pixel) + vec2(0.5)) / vec2(resolution);
    vec3  P      = compute_world_position(uv, depth, camera.data.invVP);
    #endif

    // fetch surfel grid
    int3 gridIndex = compute_surfel_grid(camera.data, P);
    uint cellIndex = compute_surfel_cell(gridIndex);
    SurfelGridCell cell = surfelGrids.data[cellIndex];

    float coverage = 0.0;
    vec4 color = vec4(0.0);
    vec4 debug = vec4(0.0);
    vec4 variance = vec4(0.0);
    vec4 shortMean = vec4(0.0);
    float vbbr = 0.0;
    float inconsistency = 0.0;

    // iterate through all surfels in this grid cell
    for (uint i = cell.offset; i < cell.offset + cell.count; i++) {
        uint cellIndex = surfelCells.data[i];
        Surfel surfel = surfels.data[cellIndex];
        SurfelData sdata = surfelData.data[surfelLive.data[cellIndex]];

        // apply surfel coverage
        vec3 L = surfel.position - P;
        float d2 = dot(L, L);
        float r2 = surfel.radius * surfel.radius;

        if (d2 < r2) {
            vec3 surfelN = unpack_snorm3(surfel.normal);
            float dotN = dot(surfelN, N);
            if (dotN > 0.0) {
                float dist = sqrt(d2);
                float contribution = 1.0;
                contribution *= clamp(dotN, 0.0, 1.0);
                contribution *= clamp(1.0 - dist / surfel.radius, 0.0, 1.0);

                float occlusion = 1.0;
                #ifdef ENABLE_SURFEL_RADIAL_DEPTH
                // radial depth function
                vec2 radialDepthPixel = compute_surfel_octmap_pixel(cellIndex, -L/dist, surfelN);
                vec2 radialDepth = sample_radial_depth(radialDepthPixel).xy;
                occlusion *= compute_surfel_weight(radialDepth, dist);
                #endif

                // surfel moment helps prevent incorrect color bleeding.
                contribution = smoothstep(0.0, 1.0, contribution);
                coverage += contribution * occlusion;
                color += vec4(surfel.color * occlusion, 1.0) * contribution;

                #ifdef ENABLE_SURFEL_DEBUG_RANDOM
                debug += vec4(random_colors[cellIndex % random_color_count], 1.0) * contribution;
                #endif

                #ifdef ENABLE_SURFEL_DEBUG_POINT
                if (dist < 0.02) {
                    debug += vec4(random_colors[cellIndex % random_color_count], 1.0);
                }
                #endif

                #ifdef ENABLE_SURFEL_DEBUG_DEPTH
                #ifdef ENABLE_SURFEL_RADIAL_DEPTH
                float w = compute_surfel_weight(radialDepth, dist);
                debug += vec4(w, w, w, 1.0);
                #endif
                #endif

                // for spawning a new surfel
                variance += vec4(sdata.variance, 1.0) * contribution;
                shortMean += vec4(sdata.shortMean, 1.0) * contribution;
                vbbr += sdata.vbbr * contribution;
                inconsistency += sdata.inconsistency * contribution;
            }
        }
        // end of pixel coverage check
    }

    // normalize color
    if (color.a > 0.0) {
        color.rgb /= color.a;
        color.a = clamp(color.a, 0.0, 1.0);
    }

    // normalize debug color
    if (debug.a > 0.0) {
        debug.rgb /= debug.a;
        debug.a = clamp(debug.a, 0.0, 1.0);
    }

    // normalize variance
    if (variance.a > 0.0) {
        vbbr /= variance.a;
        inconsistency /= variance.a;
        shortMean /= variance.a;
        variance /= variance.a;
    }

    // debug tiles
    #if 0
    if (((pixel.x & 0xf) == 0xf) || ((pixel.y & 0xf) == 0xf)) {
        color = vec4(0.0);
    }
    #endif

    // output
    #ifdef ENABLE_HALFRES_LIGHT_APPLY
    imageStore(debugImage, pixel + int2(0, 0), debug);
    imageStore(debugImage, pixel + int2(1, 0), debug);
    imageStore(debugImage, pixel + int2(0, 1), debug);
    imageStore(debugImage, pixel + int2(1, 1), debug);

    imageStore(diffuseImage, pixel + int2(0, 0), color);
    imageStore(diffuseImage, pixel + int2(1, 0), color);
    imageStore(diffuseImage, pixel + int2(0, 1), color);
    imageStore(diffuseImage, pixel + int2(1, 1), color);

    imageStore(coverageImage, pixel + int2(0, 0), vec4(vec3(coverage), 1.0));
    imageStore(coverageImage, pixel + int2(1, 0), vec4(vec3(coverage), 1.0));
    imageStore(coverageImage, pixel + int2(0, 1), vec4(vec3(coverage), 1.0));
    imageStore(coverageImage, pixel + int2(1, 1), vec4(vec3(coverage), 1.0));

    imageStore(varianceImage, pixel + int2(0, 0), vec4(variance.r, 0.0, 1.0 - variance.r, 1.0));
    imageStore(varianceImage, pixel + int2(1, 0), vec4(variance.r, 0.0, 1.0 - variance.r, 1.0));
    imageStore(varianceImage, pixel + int2(0, 1), vec4(variance.r, 0.0, 1.0 - variance.r, 1.0));
    imageStore(varianceImage, pixel + int2(1, 1), vec4(variance.r, 0.0, 1.0 - variance.r, 1.0));
    #else
    imageStore(debugImage, pixel, debug);
    imageStore(diffuseImage, pixel, color);
    imageStore(coverageImage, pixel, vec4(coverage, coverage, coverage, 1.0));
    imageStore(varianceImage, pixel, vec4(variance.r, 0.0, 1.0 - variance.r, 1.0));
    #endif

    // spawn new surfels based on coverage
    if (surfelStat.data.pause != 0) {
        return;
    }

#if 1
    // rng state
    RNGState rng = init_rng(uint2(pixel), resolution, frame.data.frameID);

    // find minimum surfel coverage
    uint2 localID = gl_LocalInvocationID.xy;
    uint covU32 = uint(coverage * 255.0);
    uint randbits = floatBitsToUint(rand(rng));
    uint coverageID = (covU32    & 0xff  ) << 24 // top bits matters the most
                    | (randbits  & 0xffff) << 8  // shuffles pixels around
                    | (localID.x & 0xf   ) << 4
                    | (localID.y & 0xf   ) << 0;
    if (cell.count < SURFEL_CELL_CAPACITY) {
        atomicMin(groupMinSurfelID, coverageID);
        atomicMin(groupMinSurfelCoverage, covU32);

        memoryBarrierShared();

        // spawn surfel
        if (depth < 1.0 && coverageID == groupMinSurfelID && coverage < SURFEL_LOW_COVERAGE) {
            // need to spawn a new surfel
            float zNear = camera.data.zNear;
            float zFar = camera.data.zFar;
            float zFarRcp = camera.data.zFarRcp;
            float linear = linearize_depth(depth, zNear, zFar) * zFarRcp;

            float threshold = pow(1.0 - linear, 3.0);
            float chance = rand(rng);

            // stochastically spawn surfel
            if (chance > threshold) {
                // request an available surfel
                uint availableSurfelID = atomicAdd(surfelStat.data.count, 1);
                uint surfelID = surfelLive.data[availableSurfelID];

                // allocate new surfel
                SurfelData data;
                #ifdef ENABLE_GBUFFER_WORLD_POSITION
                data.position = P;
                #else
                data.position = P + 0.01 * N;
                #endif
                data.normal = pack_snorm3(N);
                data.life = 0;
                data.recycle = 0;
                data.vbbr = vbbr;
                data.inconsistency = inconsistency;
                data.mean = color.rgb;
                data.shortMean = shortMean.rgb;
                data.variance = variance.xyz;
                data.instanceID = primID;
                data.surfelID = surfelID;

                surfelData.data[surfelID] = data;
            }
        }
    }
#else
    if (frame.data.frameID > 1) {
        surfelStat.data.pause = 1;
    }

    // rng state
    RNGState rng = init_rng(uint2(pixel), resolution, frame.data.frameID);

    if ((pixel.x == 0) && (pixel.y == 0)) {
        float w = 0.5;

        for (int i = -50; i < 50; i++) {
            for (int j = -50; j < 50; j++) {
                // request an available surfel
                uint availableSurfelID = atomicAdd(surfelStat.data.count, 1);
                uint surfelID = surfelLive.data[availableSurfelID];

                float rnd1 = 0.15; // rand(rng) * w;
                float rnd2 = 0.15; // rand(rng) * w;

                vec3 N = vec3(0.0, 1.0, 0.0);
                vec3 P = vec3(i * w + rnd1, 0.05, j * w + rnd2);

                // allocate new surfel
                SurfelData data;
                data.position = P;
                data.normal = pack_snorm3(N);
                data.life = 0;
                data.recycle = 0;
                data.vbbr = vbbr;
                data.inconsistency = inconsistency;
                data.mean = color.rgb;
                data.shortMean = shortMean.rgb;
                data.variance = variance.xyz;
                data.instanceID = primID;
                data.surfelID = surfelID;

                surfelData.data[surfelID] = data;
            }
        }

        for (int i = -50; i < 50; i++) {
            for (int j = -50; j < 50; j++) {
                // request an available surfel
                uint availableSurfelID = atomicAdd(surfelStat.data.count, 1);
                uint surfelID = surfelLive.data[availableSurfelID];

                float rnd1 = 0.15; // rand(rng) * w;
                float rnd2 = 0.15; // rand(rng) * w;

                vec3 N = vec3(0.0, 0.0, 1.0);
                vec3 P = vec3(i * w + rnd1, j * w + rnd2, 0.03);

                // allocate new surfel
                SurfelData data;
                data.position = P;
                data.normal = pack_snorm3(N);
                data.life = 0;
                data.recycle = 0;
                data.vbbr = vbbr;
                data.inconsistency = inconsistency;
                data.mean = color.rgb;
                data.shortMean = shortMean.rgb;
                data.variance = variance.xyz;
                data.instanceID = primID;
                data.surfelID = surfelID;

                surfelData.data[surfelID] = data;
            }
        }

        for (int i = -50; i < 50; i++) {
            for (int j = -50; j < 50; j++) {
                // request an available surfel
                uint availableSurfelID = atomicAdd(surfelStat.data.count, 1);
                uint surfelID = surfelLive.data[availableSurfelID];

                float rnd1 = 0.15; // rand(rng) * w;
                float rnd2 = 0.15; // rand(rng) * w;

                vec3 N = vec3(0.0, 0.0, -1.0);
                vec3 P = vec3(i * w + rnd1, j * w + rnd2, -0.03);

                // allocate new surfel
                SurfelData data;
                data.position = P;
                data.normal = pack_snorm3(N);
                data.life = 0;
                data.recycle = 0;
                data.vbbr = vbbr;
                data.inconsistency = inconsistency;
                data.mean = color.rgb;
                data.shortMean = shortMean.rgb;
                data.variance = variance.xyz;
                data.instanceID = primID;
                data.surfelID = surfelID;

                surfelData.data[surfelID] = data;
            }
        }

        for (int i = -50; i < 50; i++) {
            for (int j = -50; j < 50; j++) {
                // request an available surfel
                uint availableSurfelID = atomicAdd(surfelStat.data.count, 1);
                uint surfelID = surfelLive.data[availableSurfelID];

                float rnd1 = 0.15; // rand(rng) * w;
                float rnd2 = 0.15; // rand(rng) * w;

                vec3 N = vec3(1.0, 0.0, 0.0);
                vec3 P = vec3(0.03, i * w + rnd1, j * w + rnd2);

                // allocate new surfel
                SurfelData data;
                data.position = P;
                data.normal = pack_snorm3(N);
                data.life = 0;
                data.recycle = 0;
                data.vbbr = vbbr;
                data.inconsistency = inconsistency;
                data.mean = color.rgb;
                data.shortMean = shortMean.rgb;
                data.variance = variance.xyz;
                data.instanceID = primID;
                data.surfelID = surfelID;

                surfelData.data[surfelID] = data;
            }
        }

        for (int i = -50; i < 50; i++) {
            for (int j = -50; j < 50; j++) {
                // request an available surfel
                uint availableSurfelID = atomicAdd(surfelStat.data.count, 1);
                uint surfelID = surfelLive.data[availableSurfelID];

                float rnd1 = 0.15; // rand(rng) * w;
                float rnd2 = 0.15; // rand(rng) * w;

                vec3 N = vec3(-1.0, 0.0, 0.0);
                vec3 P = vec3(-0.03, i * w + rnd1, j * w + rnd2);

                // allocate new surfel
                SurfelData data;
                data.position = P;
                data.normal = pack_snorm3(N);
                data.life = 0;
                data.recycle = 0;
                data.vbbr = vbbr;
                data.inconsistency = 1;
                data.mean = vec3(0.0);
                data.shortMean = shortMean.rgb;
                data.variance = variance.xyz;
                data.instanceID = primID;
                data.surfelID = surfelID;

                surfelData.data[surfelID] = data;
            }
        }
    }
#endif
}
